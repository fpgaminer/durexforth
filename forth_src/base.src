: hex 16 base ! ;
: dec 10 base ! ;
hex
: bl 20 ;
: cr d emit ;
: space bl emit ;
: / /mod swap drop ;
: mod /mod drop ;
: negate 0 swap - ;
: bg d020 c! ;
: literal immed ' lit , , ;
: '(' [ char ( ] literal ;
: ')' [ char ) ] literal ;
: [compile] immed word find >cfa , ;
: if immed ' 0branch , here @ 0 , ;
: then immed dup here @ swap - swap ! ;
: else immed ' branch , here @ 0 , swap dup here @ swap - swap ! ;
: green 5 bg ;
: yellow 7 bg ;
: begin immed here @ ;
: until immed ' 0branch , here @ - , ;
: again immed ' branch , here @ - , ;
: while immed ' 0branch , here @ 0 , ;
: repeat immed ' branch , swap here @ - , dup here @ swap - swap ! ;
: unless immed ' not , [compile] if ;
: recurse immed latest @ >cfa , ;
: incbg d020 @ 1+ d020 c! ;
: decbg d020 @ 1- d020 c! ;
: ( immed begin key ')' = until ;
( so... it looks like i finally got comments working... no? )
: cc begin incbg 0 until ;
: nip ( x y -- y ) swap drop ;
: tuck ( x y -- y x y ) dup rot ;
: <> ( n -- b ) = not ;
: > ( n -- b ) swap < ;
: 0> ( n -- b ) 0 > ;
: 0< ( n -- b ) 0 < ;
: 0<= ( n -- b ) dup 0= swap 0< or ;
: 0>= ( n -- b ) dup 0= swap 0> or ;
: spaces ( n -- ) begin dup 0> while space 1- repeat drop ;

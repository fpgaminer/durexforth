;The MIT License
;
;Copyright (c) 2013 Johan Kotlinski
;
;Permission is hereby granted, free of charge, to any person obtaining a copy
;of this software and associated documentation files (the "Software"), to deal
;in the Software without restriction, including without limitation the rights
;to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
;copies of the Software, and to permit persons to whom the Software is
;furnished to do so, subject to the following conditions:
;
;The above copyright notice and this permission notice shall be included in
;all copies or substantial portions of the Software.
;
;THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
;IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
;FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
;AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
;LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
;OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
;THE SOFTWARE.

; Methods for number parsing

; base is single-byte variable.
; allowed values: 10, 16
    +BACKLINK
	!byte 4
	!text	"base"
BASE
	+VAR_CODE	_BASE
_BASE
    !word 16

; Z = success, NZ = fail
; success: ( string ptr -- number )
; fail: ( string ptr -- string ptr )
READ_NUMBER
	lda	1, x	
	sta	TMP3 + 1
	lda	0, x	
	sta	TMP3
    ; TMP3 now points to string length
    ; followed by string.
    ldy #0
    sty TMP
    sty TMP + 1
    lda _BASE
    cmp #16
    beq READ_HEX_NUMBER
    cmp #10
    beq READ_DEC_NUMBER
    cmp #2
    beq READ_BIN_NUMBER
    rts

READ_DEC_NUMBER
	lda	(TMP3), y
    cmp #6  ; Max 5 digits.
    bcs .not_a_digit
	sta	.chars_to_process

.read_dec_digit
	; *= 10
    asl TMP ; *2
    rol TMP + 1
    lda TMP
    sta TMP2
    lda TMP + 1
    sta TMP2 + 1
    asl TMP ; *4
    rol TMP + 1
    asl TMP ; *8
    rol TMP + 1
    lda TMP
    adc TMP2
    sta TMP
    lda TMP + 1
    adc TMP2 + 1
    sta TMP + 1

	; add *(++TMP3)
	iny
	lda	(TMP3), y
	clc
	adc	#-$30 ; petscii 0-9 -> 0-9

	sec ; within 0-9?
	cmp	#10
	bcs	.not_a_digit

    adc TMP
    sta TMP
    bcc +
    inc TMP + 1
+
    dec .chars_to_process
    bne .read_dec_digit
    jmp got_number

; Z = success, NZ = fail
READ_BIN_NUMBER
	lda	(TMP3), y
    cmp #17  ; Max 16 digits.
    bcs .not_a_digit
	sta	.chars_to_process

.read_bin_digit
    asl TMP ; *= 2
    rol TMP + 1

	; add *(++TMP3)
	iny
	lda	(TMP3), y
	clc
	adc	#-$30 ; petscii 0-1 -> 0-1

	sec ; within 0-1?
	cmp	#2
	bcs	.not_a_digit

    ora TMP
    sta TMP

    dec .chars_to_process
    bne .read_bin_digit
    jmp got_number

.not_a_digit
    lda #$ff
    rts

; Z = success, NZ = fail
READ_HEX_NUMBER
	lda	(TMP3), y
    cmp #5  ; Max 4 digits.
    bcs .not_a_digit
	sta	.chars_to_process

.read_hex_digit
	; *= 16
    asl TMP
    rol TMP + 1
    asl TMP
    rol TMP + 1
    asl TMP
    rol TMP + 1
    asl TMP
    rol TMP + 1

	; add *(++TMP3)
	iny
	lda	(TMP3), y
	clc
	adc	#-$30 ; petscii 0-9 -> 0-9

	sec ; within 0-9?
	cmp	#10
	bcc	.got_hex_digit

	clc
	adc	#-$7 ; a-f...

	sec ; within a-f?
	cmp	#16
	bcs	.not_a_digit
	sec
	cmp	#10
	bcc	.not_a_digit

.got_hex_digit
    ora TMP
    sta TMP

    dec .chars_to_process
    bne .read_hex_digit

got_number
    lda TMP
    sta 0, x
    lda TMP + 1
    sta 1, x
    lda #0
    rts

.chars_to_process
!byte 0

